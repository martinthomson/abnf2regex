; This ABNF is psychotic.  It is intended to represent the 8-bit values used in unicode.
; As such, an NAI has no direct way to be represented in unicode due to the \x construct.

nai         =  username
nai         =/ [ username ] "@" realm

username    =  dot-string
dot-string  =  string *( "." string)
string      =  *nai-char
nai-char    =  c
nai-char    =/ "\" x

c           =  %x21    ; '!'              allowed
                       ; '"'              not allowed
c           =/ %x23    ; '#'              allowed
c           =/ %x24    ; '$'              allowed
c           =/ %x25    ; '%'              allowed
c           =/ %x26    ; '&'              allowed
c           =/ %x27    ; '''              allowed
                       ; '(', ')'         not allowed
c           =/ %x2A    ; '*'              allowed
c           =/ %x2B    ; '+'              allowed
                       ; ','              not allowed
c           =/ %x2D    ; '-'              allowed
                       ; '.'              not allowed
c           =/ %x2F    ; '/'              allowed
c           =/ %x30-39 ; '0'-'9'          allowed
                       ; ';', ':', '<'    not allowed
c           =/ %x3D    ; '='              allowed
                       ; '>'              not allowed
c           =/ %x3F    ; '?'              allowed
                       ; '@'              not allowed
c           =/ %x41-5a ; 'A'-'Z'          allowed
                       ; '[', '\', ']'    not allowed
c           =/ %x5E    ; '^'              allowed
c           =/ %x5F    ; '_'              allowed
c           =/ %x60    ; '`'              allowed
c           =/ %x61-7A ; 'a'-'z'          allowed
c           =/ %x7B    ; '{'              allowed
c           =/ %x7C    ; '|'              allowed
c           =/ %x7D    ; '}'              allowed
c           =/ %x7E    ; '~'              allowed
                       ; DEL              not allowed
c           =/ %x80-FF ; UTF-8-Octet      allowed (not in RFC 2486)
                       ; Where UTF-8-octet is any octet in the
                       ; multi-octet UTF-8 representation of a
                       ; unicode codepoint above %x7F.
                       ; Note that c must also satisfy rules in
                       ; Section 2.4, including, for instance,
                       ; checking that no prohibited output is
                       ; used (see also Section 2.3 of
                       ; [RFC4013]).
x           =  %x00-FF ; all 128 ASCII characters, no exception;
                       ; as well as all UTF-8-octets as defined
                       ; above (this was not allowed in
                       ; RFC 2486).  Note that x must nevertheless
                       ; again satisfy the Section 2.4 rules.

realm       =  1*( label "." ) label
label       =  let-dig *(ldh-str)
ldh-str     =  *( alpha / digit / "-" ) let-dig
let-dig     =  alpha / digit
alpha       =  %x41-5A  ; 'A'-'Z'
alpha       =/ %x61-7A  ; 'a'-'z'
digit       =  %x30-39  ; '0'-'9'
